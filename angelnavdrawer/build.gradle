apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

//https://jitpack.io/docs/BUILDING/
def vers = '1.1.4'

android {

    namespace =  'angelandroidapps.twitch.angelnavdrawer'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 33
        versionCode 320002
        versionName vers

        setProperty("archivesBaseName", "angelSimpleNavDrawer-$vers")
        consumerProguardFiles "consumer-rules.pro"
    }


/*
version 320000+: maven-publish
version 6: updatable logo and title
version 5: nightMode
 */

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'


}

//https://developer.android.com/studio/publish-library/upload-library
//shows in <users>/.m2/repository/<groupId> when compile in gradlew -publishToMavenLocal
publishing {
    publications {
        // Creates a Maven publication called "release".
        release(MavenPublication) {
            groupId = 'com.github.angelkoh'
            artifactId = 'angel-simple-nav-drawer'
            version = vers

            afterEvaluate {
                from components.release
            }
        }
    }
}